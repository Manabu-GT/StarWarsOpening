apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    // Gets the revision number from the Git
    def getRevision = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', 'HEAD'
                standardOutput = stdout
            }
            // pick first six characters
            return stdout.toString().trim().substring(0, 6)
        } catch (Exception e) {
            return ""
        }
    }

    defaultConfig {
        applicationId "com.ms_square.starwars.opening"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            // double check if keystore.properties exists to avoid exception
            if (file("../keystore.properties").exists()) {
                keyProps.load(new FileInputStream(file('../keystore.properties')))
            }
            storeFile keyProps["storeFile"] != null ? file(keyProps["storeFile"]) : null
            storePassword keyProps["storePassword"]
            keyAlias keyProps["keyAlias"]
            keyPassword keyProps["keyPassword"]
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "BUILD_NUMBER", "\"" + getRevision() + "\""
            zipAlignEnabled true
            minifyEnabled false
        }
        release {
            buildConfigField "String", "BUILD_NUMBER", "\"" + getRevision() + "(R)\""
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // check if keystore.properties exists in the root
            if (file("../keystore.properties").exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    // v7 includes vector drawable support library
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:percent:23.4.0'
}
